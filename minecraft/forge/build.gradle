
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.modrinth.minotaur'
apply plugin: 'com.matthewprenger.cursegradle'

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":${project.module_name}:common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
        mixinConfig "playerAnimator-common.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.forge_version}"

    //modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":${project.module_name}:common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":${project.module_name}:common", configuration: "transformProductionForge")) { transitive = false }

    common(shadowCommon(project(path: ":coreLib")) {transitive false}) {transitive false} //Why can I nest these?

    //Testing libraries
    //*
    modLocalRuntime "io.github.kosmx.bendy-lib:bendy-lib-forge:${project.bendy_lib}"
    modLocalRuntime "maven.modrinth:3dskinlayers:1.5.2-forge-1.19"
    modLocalRuntime "maven.modrinth:emotecraft:2.2.6-SNAPSHOT-build.44-MC1.19.2-forge"
    //*/
}

project.archivesBaseName = rootProject.archives_base_name + "-" + project.name

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier.set("dev-shadow")
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier.set(null)
}

jar {
    archiveClassifier.set("dev")
}

sourcesJar {
    def commonSources = project(":${project.module_name}:common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }

    def apiSources = project(":coreLib").sourcesJar
    dependsOn apiSources
    from apiSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        repositories {

            if (project.keysExists) {
                maven {
                    url = 'https://maven.kosmx.dev/'
                    credentials {
                        username = project.keys.kosmx_maven_user
                        password = project.keys.kosmx_maven
                    }
                }
                maven {
                    name = "GitHubPackages"
                    url = "https://maven.pkg.github.com/kosmx/minecraftPlayerAnimator"
                    credentials {
                        username = System.getenv("GITHUB_ACTOR")
                        password = System.getenv("GITHUB_TOKEN")
                    }
                }
            } else {
                mavenLocal()
            }
        }
    }
}


if(keysExists) {
    modrinth {
        versionType = project.cfType

        uploadFile = remapJar

        token = project.keys.modrinth_token

        projectId = "playeranimator"

        versionNumber = "${rootProject.mod_version}-forge"
        versionName = "${rootProject.mod_version}-forge"

        gameVersions = ["1.19", "1.19.1", "1.19.2"]
        changelog = changes
        loaders = ["forge"]
        failSilently = false

        dependencies {
            optional.project "bendy-lib"
        }
    }

    curseforge {
        apiKey = project.keys.curseforge_key

        project {
            id = '658587'
            changelogType = "markdown"
            //changelog = '[See on Github](https://github.com/KosmX/emotes/commits/master)'
            changelog = changes
            releaseType = project.cfType
            addGameVersion "1.19"
            addGameVersion "1.19.1"
            addGameVersion "1.19.2"
            addGameVersion "Forge"


            relations {
                optionalDependency 'bendy-lib'
            }

            options {
                forgeGradleIntegration = false
                javaVersionAutoDetect = false // defaults to true
            }


            mainArtifact(remapJar)
        }//*/
    }
}
