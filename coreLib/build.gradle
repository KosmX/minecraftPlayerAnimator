plugins {
    id 'java-library'
    id 'maven-publish'
    //id "io.freefair.lombok" version "6.5.0.2" This breaks javadoc (???) I'll use annotationProcessor
}

dependencies {

    implementation 'com.google.code.gson:gson:2.8.9'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'


    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
    compileOnly 'org.jetbrains:annotations:23.0.0'
}

compileJava {
    options.release.set 8
}


test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'anim-core'

            from components.java

            pom{
                name = "playerAnimatorCORE"
                description = "Minecraft animation api"
                url = 'https://github.com/KosmX/emotes'
                developers {
                    developer {
                        id = 'kosmx'
                        name = 'KosmX'
                        email = 'kosmx.mc@gmail.com'
                    }
                }
            }
        }
    }
    repositories {

        if (project.keysExists) {
            maven {
                url = 'https://maven.kosmx.dev/'
                credentials {
                    username = project.keys.kosmx_maven_user
                    password = project.keys.kosmx_maven
                }
            }
            maven {
                name = "GitHubPackages"
                url = "https://maven.pkg.github.com/kosmx/minecraftPlayerAnimator"
                credentials {
                    username = System.getenv("GITHUB_ACTOR")
                    password = System.getenv("GITHUB_TOKEN")
                }
            }
        } else {
            mavenLocal()
        }
    }
}